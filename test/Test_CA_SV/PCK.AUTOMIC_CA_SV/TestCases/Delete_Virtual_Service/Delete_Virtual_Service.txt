*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        delete_virtual_service
Test Template     Delete Virtual Service Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VSEName           VirtualServiceName         FailIfVSDoesNotExist
Delete Virtual Service with valid VSE Name & Virtual Service Name
                      [Tags]    main_scenarios
                      OK        ${VSE_Name}       ${Virtual_Service_Name}    YES

Delete Virtual Service with valid VSE Name & Invalid Virtual Service Name
                      OK        ${VSE_Name}       Invalid                    NO

Delete Virtual Service with invalid VSE Name & valid Virtual Service Name
                      NOK       Invalid           ${Virtual_Service_Name}    YES

Delete Virtual Service with valid VSE Name & invalid Virtual Service Name
                      NOK       ${VSE_Name}       Invalid                    YES

Assert VSE Name as Mandatory Field
                      NOK       ${EMPTY}          ${Virtual_Service_Name}    YES

Assert Virtual Service Name as Mandatory Field
                      NOK       ${VSE_Name}       ${EMPTY}                   YES

*** Keywords ***
Startup
    Log    *******Start Test "DELETE VIRTUAL SERVICE"*******
    Log    *******Connect to AE*******
    Connect AE

Delete Virtual Service Template
    [Arguments]    ${OK_NOK}    ${VSEName}    ${VirtualServiceName}    ${FailIfVSDoesNotExist}
    [Documentation]    This action deletes the specified Virtual Service.If you specify "Fail If VS does not exist" to Yes, Action will fail if specified service does not exist.If you specify "Fail if VS does not exist" to No, Action will end successfully if specified service does not exist.
    ...    -Precondition: To execute valid test, Valid VSE Name & Virtual Service Name should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${VSEName}: This field specifies name of the VS Environment in which VS is found.
    ...    -${VirtualServiceName}: This field specifies name of the Virtual Service that you want to delete.
    ...    -${FailIfVSDoesNotExist}: This field specifies No, if you don't want to fail the action if specified service does not exist.
    Generate String
    Init Action    ${_DELETE_VIRTUAL_SERVICE}
    Connect To SV
    Run Keyword If    '${VSEName}' == 'Invalid'    Action Set    &UC4RB_SV_VS_ENV#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_SV_VS_ENV#    ${VSEName}
    Run Keyword If    '${VirtualServiceName}' == 'Invalid'    Action Set    &UC4RB_SV_VS_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_SV_VS_NAME#    ${VirtualServiceName}
    Action Set    &UC4RB_SV_VS_EXIST#    ${FailIfVSDoesNotExist}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "DELERE VIRTUAL MACHINE"*******
