*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        run_test_suite
Test Template     Run Test Suite Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    TestSuitePath         ConfigPath        RunAsynchronously
Run Test Suite with providing Valid Test case path & Config path
                      [Tags]    main_scenarios
                      OK        ${Test_Suite_Path}    ${Config_Path}    YES

Run Test Suite with providing Valid Test case path & widout providing Config path
                      OK        ${Test_Suite_Path}    ${EMPTY}          YES

Run Test with providing invalid Test case path and Async Call as Yes
                      OK        Invalid               ${Config_Path}    YES

Run Test with providing invalid Test case path
                      [Tags]    main_scenarios
                      NOK       Invalid               ${Config_Path}    NO

Run Test with providing invalid Staging doc path
                      NOK       ${Test_Suite_Path}    Invalid           NO

Assert Test Suite Path as Mandatory Field
                      NOK       ${EMPTY}              ${Config_Path}    NO

*** Keywords ***
Startup
    Log    *******Start Test "RUN TEST SUITE"*******
    Log    *******Connect to AE*******
    Connect AE

Run Test Suite Template
    [Arguments]    ${OK_NOK}    ${TestSuitePath}    ${ConfigPath}    ${RunAsynchronously}
    [Documentation]    This action runs the Test Suite. You need to provide atleast the full file path to the Test Suite (.ste file). You could also provide the Configuration file to be used when executing the test suite.
    ...    -Precondition: To execute valid test, Valid Test suit path should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${TestSuitePath}: This field specifies full file path to the Test suite (.ste) file.
    ...    -${ConfigPath}: This field specifies full file path to the config file.
    ...    -${RunAsynchronously}: This field specifies if invocation should be done asynchronously or not.
    Generate String
    Init Action    ${_RUN_TEST_SUITE}
    Connect To SV Test
    Run Keyword If    '${TestSuitePath}' == 'Invalid'    Action Set    &UC4RB_SV_TEST_SUITE_FILE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_SV_TEST_SUITE_FILE#    ${TestSuitePath}
    Run Keyword If    '${ConfigPath}' == 'Invalid'    Action Set    &UC4RB_SV_CONFIG_FILE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_SV_CONFIG_FILE#    ${ConfigPath}
    Action Set    &UC4RB_SV_ASYNC#    ${RunAsynchronously}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_SV_TESTRESPONSE#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_SV_TESTRESPONSE#

Teardown
    Log    *******End Test "RUN TEST SUITE"*******
