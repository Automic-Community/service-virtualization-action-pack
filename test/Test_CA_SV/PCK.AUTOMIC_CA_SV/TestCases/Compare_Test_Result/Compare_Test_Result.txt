*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        compare_test_result
Test Template     Compare Test Result Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    BaselineXMLString        TestXMLString
Compare Test Result with valid Baseline XMLString and Test XMLString when both have same content
                      [Tags]    main_scenarios
                      OK        ${Baseline_XMLString}    ${Test_XMLString1}

Compare Test Result with valid Baseline XMLString and Test XMLString when both have different content
                      OK        ${Baseline_XMLString}    ${Test_XMLString2}

Compare Test Result with invalid Baseline XMLString and valid Test XMLString
                      NOK       Invalid                  ${Test_XMLString1}

Compare Test Result with valid Baseline XMLString and invalid Test XMLString
                      NOK       ${Baseline_XMLString}    Invalid

Assert Baseline XMLString as Mandatory Field
                      NOK       ${EMPTY}                 ${Test_XMLString1}

Assert Test XMLString as Mandatory Field
                      NOK       ${Baseline_XMLString}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "COMPARE TEST RESULT"*******
    Log    *******Connect to AE*******
    Connect AE

Compare Test Result Template
    [Arguments]    ${OK_NOK}    ${BaselineXMLString}    ${TestXMLString}
    [Documentation]    This action compares the actions results provided in the form of XML string.
    ...    -Precondition: To execute valid test, Valid Baseline XMLString & Test XMLString should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${BaselineXMLString}: This field specifies baseline test response as XML string.
    ...    -${TestXMLString}: Provide another test response as XML string to compare.
    Generate String
    Init Action    ${_COMPARE_TEST_RESULT}
    Run Keyword If    '${BaselineXMLString}' == 'Invalid'    Action Set    &UC4RB_SV_BASELINE_XML#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_SV_BASELINE_XML#    ${BaselineXMLString}
    Run Keyword If    '${TestXMLString}' == 'Invalid'    Action Set    &UC4RB_SV_TEST_XML#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_SV_TEST_XML#    ${TestXMLString}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_SV_COMPARE_RESULT#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_SV_COMPARE_RESULT#

Teardown
    Log    *******End Test "COMPARE TEST RESULT"*******
