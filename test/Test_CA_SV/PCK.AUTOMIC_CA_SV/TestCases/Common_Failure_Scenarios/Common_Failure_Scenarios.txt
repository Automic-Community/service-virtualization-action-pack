*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        common_failure_scenarios
Test Template     Common Failure Scenarios Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    APIEndpoint       Username        Password       SkipSSL    VSEName        VirtualServiceName
Assertion with Invalid Server URL
                      [Tags]    main_scenarios
                      NOK       Invalid           ${User_name}    ${Password}    YES        ${VSE_Name}    ${Virtual_Service_Name}

Assertion with invalid Username
                      NOK       ${SV_URL}         Invalid         ${Password}    YES        ${VSE_Name}    ${Virtual_Service_Name}

Assertion with Invalid Password
                      NOK       ${SV_URL}         ${User_name}    Invalid        YES        ${VSE_Name}    ${Virtual_Service_Name}

Assert Server URL as mandatory field
                      NOK       ${EMPTY}          ${User_name}    ${Password}    YES        ${VSE_Name}    ${Virtual_Service_Name}

Assert Username as mandatory field
                      NOK       ${SV_URL}         ${EMPTY}        ${Password}    YES        ${VSE_Name}    ${Virtual_Service_Name}

Assert Password as mandatory field
                      NOK       ${SV_URL}         ${User_name}    ${EMPTY}       YES        ${VSE_Name}    ${Virtual_Service_Name}

*** Keywords ***
Startup
    Log    *******Start Test "COMMON FAILURE SCENARIOS"*******
    Log    *******Connect to AE*******
    Connect AE

Common Failure Scenarios Template
    [Arguments]    ${OK_NOK}    ${APIEndpoint}    ${Username}    ${Password}    ${SkipSSL}    ${VSEName}
    ...    ${VirtualServiceName}
    [Documentation]    This test executes the common failure scenarios for action pack with start virtual service.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK, NOK.
    ...    -${APIEndpoint}: This field Specifies Service Virtualization REST API endpoint.
    ...    -${Username}:Specify username for authentication
    ...    -${Password}: This field specifies password for authentication.
    ...    -${SkipSSL}: This field specifies YES, if you want to skip SSL Validation.
    ...    -${VSEName}: This field specifies name of the VS Environment in which VS is found.
    ...    -${VirtualServiceName}: This field specifies name of the Virtual Service that you want to start.
    Generate String
    Init Action    ${_START_VIRTUAL_SERVICE}
    Run Keyword If    '${APIEndpoint}' == 'Invalid'    Action Set    &UC4RB_SV_BASE_URL#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_SV_BASE_URL#    ${APIEndpoint}
    Run Keyword If    '${Username}' == 'Invalid'    Action Set    &UC4RB_SV_USERNAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_SV_USERNAME#    ${Username}
    Run Keyword If    '${Password}' == 'Invalid'    Action Set    &UC4RB_SV_PASSWORD#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_SV_PASSWORD#    ${Password}
    Action Set    &UC4RB_SV_SSL_VALIDATION#    ${SkipSSL}
    Action Set    &UC4RB_SV_VS_ENV#    ${VSEName}
    Action Set    &UC4RB_SV_VS_NAME#    ${VirtualServiceName}
    Action Execute
    Assert Failure

Teardown
    Log    *******End Test "COMMON FAILURE SCENARIOS"*******
